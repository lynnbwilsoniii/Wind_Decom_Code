	SUBROUTINE EBPOL(EN2,ER,EL,EP,COSTK,E,B)
C
C	CALCULATES E (NORMALIZED TO 1.) AND B IN THE WAVES
C	
	COMPLEX EN2,ER,EL,EP,EPERP,D,XI,DEN,EN,E(3),B(3)
	DATA XI /(0.,1.)/
C
	SINTK2 = AMAX1(1.-COSTK**2,0.)
	SINTK = SQRT(SINTK2)
	D = .5*(ER-EL)
	EPERP = .5*(ER+EL)
	ENORM = AMAX1(CABS(ER),CABS(EL),CABS(EP))
C	ASSUME EX IS NOT ZERO AND DO RATIOS TO EX
	ERR = 1.E-7*ENORM
	DEN = EN2*SINTK2 - EP
	IF(CABS(DEN).LT.ERR) GO TO 100
	E(3) = EN2*SINTK*COSTK/DEN
   5	DEN = EN2 - EPERP
	IF(CABS(DEN).LT.ERR) GO TO 120
	E(2) = XI*D/DEN
	E(1) = 1.
	GO TO 200
C
 100	CONTINUE
C	EX IS 0. UNLESS N**2*COSTK*SINTK IS 0.
	IF(CABS(EN2)*COSTK*SINTK.GT.ERR) GO TO 110
	E(3) = 0.
	GO TO 5
 110	CONTINUE
C	EX IS 0.
	E(1) = 0.
	E(3) = 1.
	E(2) = -XI*EN2*COSTK*SINTK*E(3)/D
	GO TO 200
 120	CONTINUE
C	A STRANGE CASE. EN2 - EPERP = 0., SO EX = 0.
	E(2) = 1.
	E(1) = 0.
	E(3) = 0.
C	IF(CABS(EP-EN2*SINTK2).GT.ERR) E(3) = EN2*SINTK*COSTK
	DEN = EN2*COSTK*SINTK
	IF(CABS(DEN).GT.ERR) E(3) = XI*D*E(2)/DEN
C
C	NORMALIZE E TO LARGEST COMPONENT = (1.,0.)
C
 200	E1 = CABS(E(1))
	E2 = CABS(E(2))
	E3 = CABS(E(3))
	DEN = E(1)
	IF(E2.GT.E1.AND.E2.GT.E3) DEN = E(2)
	IF(E3.GT.E1.AND.E3.GT.E2) DEN = E(3)
	E(1) = E(1)/DEN
	E(2) = E(2)/DEN
	E(3) = E(3)/DEN
C
C	CALCULATE B
C
	EN = CSQRT(EN2)
	B(3) =  EN*SINTK*E(2)
	B(1) = -EN*COSTK*E(2)
	B(2) = EN*(COSTK*E(1) - SINTK*E(3))
	RETURN
	END
