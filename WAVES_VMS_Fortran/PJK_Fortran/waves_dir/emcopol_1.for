	PROGRAM EMCOPOL
C	SOLVES COLD PLASMA DISPERSION EQUATION, NOT ELECTROSTATIC
C	IN STIX'S NOTATION ER IS R,EL IS L,EP IS P,EPERP IS S
C	EPSL IS A
C	MODIFIED ON 12-18-92 BY JERRY THIESSEN TO WRITE OUT A FILE CONTAINING
C	THE PHASE INFORMATION. (FOR026.dat)  THIS FILE IS WRITTEN WITH 
C	REPEATED WRITES OF FIVE FORMATS--600 OR 601 AND THE OTHERS
C	  600 FORMAT(' PLUS  K=',2E13.4,' E-FIELD PHASES',3(E13.4,E13.4))
C		OR
C	  601 FORMAT(' MINUS K=',2E13.4,' E-FIELD PHASES',3(E13.4,E13.4))
C		AND
C	  604 FORMAT('   B-FIELD PHASES ',3(E13.4,E13.4))
C	  599 FORMAT('   FHZ,(EN2,XK)P&M'F12.4,4(E13.4,E12.4))
C	  602 FORMAT('   R top  ',6e13.4)
C	  603 FORMAT('   R DIAG ',6e13.4)

C
	COMPLEX CNU,DN,FN,BN,EPSL,EN2P,RMATRX(3,3)
	COMPLEX EN2M,XKP,XKM,ERN,ELN,EPN
	COMPLEX XK,EX,EY,EZ,BX,BY,BZ,IM,XMU2	! IM IS PURE IMAGINARY NUMBER
	COMPLEX ER,EL,EP,EPERP,D,EPERPN,LAMBDA,ZPL,SQL
 	REAL NU,ZERO,XC
C
	DIMENSION WP(10),WC(10),NU(10),A(10),DENS(10),WP2(10)
	DATA NC /2/
C	DATA NU /1.E-5,1.E-7,8*0./
	DATA NU /10*0./                   ! collision frequencies
	DATA ZERO /2.E-9/		! ASSUME SMALLER NUMBERS ARE 0.0
	DATA XC /1./			! SPEED OF LIGHT 
	DATA A /10*1./                    ! at. wts of ions, A(1) replace below
	DATA DENS /1.,9*1./               ! relative number densities
	DATA WCYCL /1.5/                  ! ratio of fce to fp
	DATA TWOPI /6.28318531/
	DATA IM /(0.,1.)/
	DATA RSH /13.5/
	DATA ALT /400./
	DATA FP /240./                    ! plasma freq. in Hz
	DATA COSTK /1./                   ! cos angle k to B
c	DATA COSTK /.9994/                 ! 2. degrees
c	DATA COSTK /.7071068/              ! 45 deg.
c	DATA COSTK /.0175/                 ! 89 degrees
C	W,WC,WP2 ARE RATIO TO PLASMA FREQ. OF COMP. 1
C
C	NU(1) = 700.*EXP(-ALT/313.)
C	NU(2) = 70.*EXP(-ALT/87.)
C	NU(3) = 4.*NU(2)
	PRINT 701, ALT,FP,NU(1),NU(2)
 701	FORMAT('0ALT=',F8.1,' KM, FP=',E10.3,' HZ, E,I COLL F',2E10.3)
	PRINT 703, COSTK,(A(N),N=2,NC)
 703	FORMAT(' COS THETA ='F7.4,3X,'ION AT.WTS.',10F5.1)
	PRINT 704,(DENS(N),N=2,NC)
 704	FORMAT(20X,'ION REL.DENS.',10F5.3)
	PRINT 702
 702	FORMAT('0')
  	A(1) = -1./1836.
	DO 11 N = 1,NC
	WP2(N) = DENS(N)/1836./ABS(A(N))
	WC(N) = WCYCL/1836./A(N)
 11	CONTINUE
	W = .3/FP
	DO 1 NIT = 1,200
	W=W*1.05
	FHZ = FP*W
C     CALCULATE EPSILON 
        ER = 1. 
        EL = 1. 
        EP = 1. 
        EMAG = 1. 
        DO 10 N = 1,NC  
C     REPLACE M BY M*(1. - IM*NU/W)
          CNU = CMPLX(0.,NU(N))/FP/TWOPI
          ER = ER - WP2(N)/W/(W-CNU+WC(N))
          EL = EL - WP2(N)/W/(W-CNU-WC(N))
          EP = EP - WP2(N)/W/(W-CNU)
          EMAG = AMAX1(EMAG,CABS(ER),CABS(EL),CABS(EP)) 
 10     CONTINUE
	ERN = ER/EMAG
	ELN = EL/EMAG
	EPN = EP/EMAG
        EPERPN = .5*(ERN + ELN)
        EPERP = .5*(ER + EL)
	DN = .5*(ERN - ELN)
	D  = .5*(ER  -  EL)
	COSTK2 = COSTK*COSTK
	SINTK2 = 1. - COSTK2
	SINTK  = SQRT(SINTK2)
	FN = CSQRT(((ERN*ELN-EPN*EPERPN)*SINTK2)**2
     1    + 4.*(EPN*DN*COSTK)**2)
	BN = ERN*ELN*SINTK2 + EPN*EPERPN*(1. + COSTK2)
	EPSL = EPERP*SINTK2 + EP*COSTK2
	EN2P = .5*EMAG**2*(BN + FN)/EPSL
	XKP = W*CSQRT(EN2P)
	EN2M = .5*EMAG*(BN - FN)/(EPSL/EMAG)
	XKM = W*CSQRT(EN2M)
C	PRINT 700,FHZ,EP,EPERP,ER,EL,EMAG
C	PRINT 700,FHZ,EPN,EPERPN,ERN,ELN
	PRINT 700,FHZ,EN2P,XKP,EN2M,XKM
C
C	THIS WRITES A FILE TO BE PLOTTED, USING EMCOPL.MGO
C	USING 'MONGO',PRINTER N OR TERMINAL N','INPUT EMCOPL.MGO'
C	'HARDCOPY',END
C
	write(25,700) FHZ,EN2P,XKP,EN2M,XKM
C
C	THIS IS THE PHASE CALCULATION SECTION....
	XK = XKP	! DO PLUS K VALUE FIRST
	DO 60 IK=1,2	! FOR BOTH THE PLUS AND MINUS K CASES
	  XMU2 = XK*XK*XC*XC/W/W
	  RMATRX(1,1) = EPERP-(XMU2*COSTK2)
	  RMATRX(1,2) = -IM*D
	  RMATRX(1,3) = XMU2*SINTK*COSTK
	  RMATRX(2,1) = -RMATRX(1,2)
	  RMATRX(2,2) = EPERP - XMU2
	  RMATRX(2,3) = (0.,0.)		! IS THE NOTATION (0.,0.) ?
	  RMATRX(3,1) = RMATRX(1,3)
	  RMATRX(3,2) = (0.,0.)
	  RMATRX(3,3) = EP - XMU2*SINTK2
cc	  PRINT 659, XK
c	  PRINT 660, XMU2,SINTK,COSTK
c	  PRINT 661, EPERP,EP,D
c	  PRINT 662, RMATRX(1,1),RMATRX(1,2),RMATRX(1,3)
c	  PRINT 663, RMATRX(1,1),RMATRX(2,2),RMATRX(3,3)
  659	  FORMAT (' K -------------->>',6E13.4)
  660	  FORMAT (' XMU2,SINTK,COSTK>>',6E13.4)
  661	  FORMAT (' EPERP,EP,D ----->>',6E13.4)
  662	  FORMAT (' R(TOP) --------->>',6E13.4)
  663	  FORMAT (' R(DIAG) -------->>',6E13.4)

	  EX = (1.,0.)	! SET EX =1. AND FIND RATIOS OF EY/EX &EZ/EX
C	  FINDING EY/EX....
	  IF (CABS(RMATRX(2,2)) .LE. ZERO) THEN
	    EY = (0.,0.)	! DON'T DIVIDE BY ZERO
	    IF (CABS(RMATRX(2,1)).GT.ZERO) TYPE 606
  606	    FORMAT (' ERROR%%% D NOT EQUAL TO ZERO')
	  ELSE
	    EY = -RMATRX(2,1)/RMATRX(2,2)
	  ENDIF
C	  FINDING EZ/EX
	  IF (CABS(RMATRX(3,3)) .LE. ZERO) THEN
	    EZ = (0.,0.)	! DON'T DIVIDE BY ZERO...
	    IF (CABS(RMATRX(3,1)) .GT. ZERO) TYPE 607
  607	    FORMAT(' ERROR%%% RMATRX(3,1) NE ZERO')
	    IF (EY.EQ.EZ) TYPE 608
  608	    FORMAT (' ERROR%%% ZERO MATRIX RMATRX     ERROR%%%')
	  ELSE
	    EZ = -RMATRX(3,1)/RMATRX(3,3)
C	    IF (EZ .NE. (-RMATRX(3,1)/RMATRX(3,3))) TYPE 609
C  609	    FORMAT (' ERROR%%% INCONSISTENT MATRIX')
	  ENDIF

C	NOW CHECK VALUES OF EX EY EZ AND SOLVE WRT THE LARGEST VALUE....
	  IF ((CABS(EY).GT.1.) .AND. (CABS(EY).GT.CABS(EZ))) THEN 
C	    SOLVE WRT EY
	    EY = (1.,0.)	! SOLVE WRT EY
	    EX = -RMATRX(2,2)/RMATRX(2,1)
	    IF (EZ.NE.(0.,0.)) EZ = -EX*RMATRX(3,1)/RMATRX(3,3)
	  ELSE IF ((CABS(EZ).GT.1.) .AND. (CABS(EZ).GT.CABS(EY))) THEN
C	    ! SOLVE WRT EZ
	    EZ = (1.,0.)
	    EX = -RMATRX(3,3)/RMATRX(3,1)
	    IF (EY .NE. (0.,0.)) EY = -EX*RMATRX(2,1)/RMATRX(2,2)
C	  ELSE ! SOLVE WRT EX--AND WE ALREADY DID THAT
	  ENDIF

C	CALCULATE MAGNETIC COMPONENTS KxE--WHERE K IS IN THE X-Z PLANE
	  BX = -XK*COSTK*EY
	  BY = XK*((COSTK*EX) - (SINTK*EZ))
	  BZ = XK*SINTK*EY

C	WHEN DONE WRITE OUTPUT INTO FILE, 
	  IF (IK.EQ.1) THEN
	    WRITE(26,600) XK,EX,EY,EZ
	  ELSE
	    WRITE(26,601) XK,EX,EY,EZ
	  ENDIF
	  WRITE(26,604) BX,BY,BZ
	  WRITE(26,599) FHZ,EN2P,XKP,EN2M,XKM
	  WRITE(26,602) RMATRX(1,1),RMATRX(1,2),RMATRX(1,3)
	  WRITE(26,603) RMATRX(1,1),RMATRX(2,2),RMATRX(3,3)
	  XK = XKM	! THEN REPEAT FOR THE MINUS K SOLUTION
 60	CONTINUE
C
 1	CONTINUE
C     PRINT 100,W,X(1),EPSR,EPS0,EP 
  100 FORMAT(' COPLAS' 8E12.4)  
  599 FORMAT('   FHZ,(EN2,XK)P&M'F12.4,4(E13.4,E12.4))
  600 FORMAT(' PLUS  K=',2E13.4,' E-FIELD PHASES',3(E13.4,E13.4))
  601 FORMAT(' MINUS K=',2E13.4,' E-FIELD PHASES',3(E13.4,E13.4))
  602 FORMAT('   R top  ',6e13.4)
  603 FORMAT('   R DIAG ',6e13.4)
  604 FORMAT('   B-FIELD PHASES ',3(E13.4,E13.4))
  700 FORMAT(' 'F12.4,4(E13.4,E12.4),E12.4)
C      RETURN
	STOP
      END 
